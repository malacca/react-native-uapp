apply plugin: 'com.android.library'

def channel = ""
(new XmlSlurper()).parse('app/src/main/AndroidManifest.xml')
    .application.'meta-data'.each {
    if (it.'@android:name'.text().equals("UMENG_CHANNEL")) {
        channel = it.'@android:value'.text()
    }
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def safeCfgGet(prop, fallback) {
    return project.hasProperty(prop) ? project.getProperties().get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 28)
    buildToolsVersion safeExtGet('buildToolsVersion', "28.0.3")
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 28)
        versionCode 1
        versionName "1.0"
        buildConfigField("int", "UMENG_DEVICE_TYPE", safeCfgGet("UMENG_DEVICE_TYPE", "1"))
        buildConfigField("String", "UMENG_PUSH_SECRET", safeCfgGet("UMENG_PUSH_SECRET", "\"\""))
        buildConfigField("String", "UMENT_PUSH_XIAOMI_ID", safeCfgGet("UMENT_PUSH_XIAOMI_ID", "\"\""))
        buildConfigField("String", "UMENT_PUSH_XIAOMI_KEY", safeCfgGet("UMENT_PUSH_XIAOMI_KEY", "\"\""))
        buildConfigField("String", "UMENT_PUSH_MEIZU_ID", safeCfgGet("UMENT_PUSH_MEIZU_ID", "\"\""))
        buildConfigField("String", "UMENT_PUSH_MEIZU_KEY", safeCfgGet("UMENT_PUSH_MEIZU_KEY", "\"\""))
        buildConfigField("String", "UMENT_PUSH_OPPO_ID", safeCfgGet("UMENT_PUSH_OPPO_ID", "\"\""))
        buildConfigField("String", "UMENT_PUSH_OPPO_KEY", safeCfgGet("UMENT_PUSH_OPPO_KEY", "\"\""))
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.facebook.react:react-native:+'

    implementation 'com.umeng.umsdk:utdid:1.1.5.3'
    implementation 'com.umeng.umsdk:common:2.1.0'
    implementation 'com.umeng.umsdk:analytics:8.1.3'
    implementation 'com.umeng.umsdk:push:6.0.1'

    if (channel == safeCfgGet("UMENT_PUSH_XIAOMI_CHANNEL", "xiaomi")) {
        implementation 'com.umeng.umsdk:xiaomi-push:3.6.17'
        implementation 'com.umeng.umsdk:xiaomi-umengaccs:1.1.0'

    } else if (channel == safeCfgGet("UMENT_PUSH_HUAWEI_CHANNEL", "huawei")) {
        implementation 'com.umeng.umsdk:huawei-basetb:2.6.3.305'
        implementation 'com.umeng.umsdk:huawei-pushtb:2.6.3.305'
        implementation 'com.umeng.umsdk:huawei-umengaccs:1.2.1'

    } else if (channel == safeCfgGet("UMENT_PUSH_MEIZU_CHANNEL", "meizu")) {
        implementation 'com.umeng.umsdk:meizu-push:3.8.1'
        implementation 'com.umeng.umsdk:meizu-umengaccs:1.0.6'

    } else if (channel == safeCfgGet("UMENT_PUSH_OPPO_CHANNEL", "oppo")) {
        implementation 'com.umeng.umsdk:oppo-push:1.0.1'
        implementation 'com.umeng.umsdk:oppo-umengaccs:1.0.3'

    } else if (channel == safeCfgGet("UMENT_PUSH_VIVO_CHANNEL", "vivo")) {
        implementation 'com.umeng.umsdk:vivo-push:2.3.1'
        implementation 'com.umeng.umsdk:vivo-umengaccs:1.0.4'

    }
}